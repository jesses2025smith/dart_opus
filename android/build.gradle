// The Android Gradle Plugin builds the native code with the Android NDK.

group = "com.example.opus_ffi"
version = "1.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath("com.android.tools.build:gradle:8.9.1")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"

def rustDir = "${projectDir}/../rust"
def abiTargets = [
        "arm64-v8a": "aarch64-linux-android",
        "x86_64"  : "x86_64-linux-android",
]
def minPlatform = 21

tasks.register("cargoNdkRelease", Exec) {
    workingDir rustDir
    def targetArgs = []
    abiTargets.values().each { targetArgs += ["--target", it] }
    commandLine(["cargo", "ndk", "--platform", "$minPlatform"] + targetArgs + ["--", "build", "--release"])
}

tasks.named("cargoNdkRelease").configure {
    doLast {
        abiTargets.each { abi, triple ->
            copy {
                from "${rustDir}/target/${triple}/release/libopus_ffi.so"
                into "${projectDir}/src/main/jniLibs/${abi}"
            }
        }
    }
}

afterEvaluate {
    tasks.named("preBuild").configure {
        dependsOn("cargoNdkRelease")
    }
}

android {
    namespace = "com.example.opus_ffi"

    // Bumping the plugin compileSdk version requires all clients of this plugin
    // to bump the version in their app.
    compileSdk = 36

    // Use the NDK version
    // declared in /android/app/build.gradle file of the Flutter project.
    ndkVersion = android.ndkVersion

    // Invoke the shared CMake build with the Android Gradle Plugin.
    externalNativeBuild {
        cmake {
            path = "../src/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdk = minPlatform
        ndk {
            abiFilters.addAll(abiTargets.keySet())
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir "${projectDir}/src/main/jniLibs"
        }
    }
}
